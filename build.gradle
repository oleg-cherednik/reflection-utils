buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

plugins {
    id 'java'
    id 'application'
    id 'jacoco'
    id 'io.codearte.nexus-staging' version '0.11.0'
}

apply plugin: 'com.bmuschko.nexus'

repositories {
    mavenLocal()
    mavenCentral()
}

group 'ru.oleg-cherednik.utils.reflection'
version '1.0'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'
mainClassName = 'unknown'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

configurations {
    jar.archiveName = "reflection-utils-${version}.jar"
}

dependencies {

    testImplementation('org.testng:testng:7.4.0') {
        exclude group: 'junit', module: 'junit'
        exclude group: 'org.apache.ant', module: 'ant'
    }

    testImplementation 'org.assertj:assertj-core:3.19.0'
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

check.dependsOn jacocoTestReport

test {
    useTestNG()
}

modifyPom {
    project {
        name 'reflection-utils'
        description 'Set of utilities for JVM to make work with reflection much easier'
        url 'https://github.com/oleg-cherednik/reflection-utils'
        inceptionYear '2021'

        scm {
            url 'https://github.com/oleg-cherednik/reflection-utils'
            connection 'scm:https://github.com/oleg-cherednik/reflection-utils.git'
            developerConnection 'scm:git@github.com:oleg-cherednik/reflection-utils.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'oleg.cherednik'
                name 'Oleg Cherednik'
                email 'oleg.cherednik@gmail.com'
            }
        }
    }
}

extraArchive {
    sources true
    tests true
    javadoc true
}

nexus {
    sign true
    repositoryUrl 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
    snapshotRepositoryUrl 'https://oss.sonatype.org/content/repositories/snapshots'
}

nexusStaging {
    packageGroup 'ru.oleg-cherednik'
    stagingProfileId 'd3835bf60f05b9'
}
